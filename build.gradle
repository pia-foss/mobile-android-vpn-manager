// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlinVersion = '1.9.0'
}

plugins {
    id 'com.android.library' version '8.4.2' apply false
    id 'org.jetbrains.kotlin.android' version "$kotlinVersion" apply false
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlinVersion"
    id 'org.jlleitschuh.gradle.ktlint' version "11.5.1"
}

allprojects {
    group = "com.kape.android"
    version = "0.3.8-pia"

    apply plugin: "org.jlleitschuh.gradle.ktlint"

    tasks.matching {it instanceof Test}.configureEach {
        testLogging.events = ["failed", "passed", "skipped"]
    }
}

subprojects {
    apply plugin: "maven-publish"
    project.afterEvaluate {
        if (!plugins.hasPlugin("android")) {
            publishing {
                publications {
                    maven(MavenPublication) {
                        afterEvaluate {
                            artifactId = "$project.name"
                            if (plugins.hasPlugin("java")) {
                                from components.java
                            } else if (plugins.hasPlugin("android-library")) {
                                from components.release
                            }
                        }
                        repositories {
                            maven {
                                url = "https://maven.pkg.github.com/pia-foss/mobile-android-vpn-manager/"
                                credentials {
                                    username = System.getenv('GITHUB_USERNAME')
                                    password = System.getenv('GITHUB_TOKEN')
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}

ext {
    // Android SDK
    targetSdkVersion = 34
    compileSdkVersion = 34
    minSdkVersion = 21

    // Implementation dependencies
    coroutinesVersion = "1.7.2"
    annotationVersion = "1.6.0"
    serializationVersion = "1.3.2"
    spongycastleCoreVersion = "1.58.0.0"
    ktorClientVersion = "2.0.0"

    // Test dependencies
    junitVersion = '4.13.2'
    testCoreVersion = '1.5.0'
    robolectricVersion = '4.10.3'
    coroutinesTestVersion = '1.7.2'
    mockkVersion = '1.13.9'
}